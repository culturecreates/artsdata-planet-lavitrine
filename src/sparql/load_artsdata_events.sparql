PREFIX schema: <http://schema.org/>
PREFIX ex: <http://example.com/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX adm: <http://kg.artsdata.ca/minted/>
PREFIX adr: <http://kg.artsdata.ca/resource/>

construct {
    ?event ?p  ?o .
    ?event a ex:EventForIndex .
    ?o ?op ?oo .
    ?oo ?oop ?ooo .
    ?ooo ?ooop ?oooo .
    ?event schema:additionalType ?selectType .
    ?event schema:location ?location .
    ?location ?loc_pred ?loc_obj ; schema:address ?address ;  schema:sameAs   ?adid .
    ?address ?address_p ?addres_o .
        
}
where {
    {
        select * where {
            <graph_placeholder> schema:hasPart | schema:hasPart/schema:hasPart ?graph .
            filter exists { adr:LaVitrineSources schema:hasPart ?graph } # had pick graphs for LaVitrine
            graph ?graph {
                {
                    select ?event 
                    where {
                        values ?type {
                             schema:Event schema:EventSeries
                        }  
                        ?event a ?type ;
                               schema:startDate ?startDate .
                        OPTIONAL {
                            ?event  schema:endDate ?endDate .
                        }
                              
                        filter( not exists {
                                ?superEvent schema:subEvent ?event
                            })
                        filter( not exists { ?event schema:additionalType adr:Festival }) 
                        filter( not exists { ?event schema:additionalType <http://lod.footlight.io/resource/65719e94a5ac3d006314ed96> }) # Mauricie
                        filter( not exists { ?event schema:additionalType <http://lod.footlight.io/resource/6418ca425e4d4e0066a2ee9f> }) # Tout Culture
                        filter(strdt(str(?startDate),xsd:dateTime) >= now() || strdt(str(?endDate),xsd:dateTime) >= now())
                    } 
                    order by ?startDate
                    limit 10 offset 0 
                }
                ?event ?p  ?o .
                ?event schema:location ?location .
                OPTIONAL {
                    ?o ?op ?oo .
                }
                OPTIONAL {
                    ?o ?op ?oo .
                    ?oo ?oop ?ooo .
                }
                OPTIONAL {
                    ?o ?op ?oo .
                    ?oo ?oop ?ooo .
                    ?ooo ?ooop ?oooo .
                    filter(!isBlank(?oooo))
                }
               
            } 
            
        }
    }
    graph ?location_graph {
        ?location schema:address ?address .
        ?address ?address_p ?addres_o .
    }
            
    
    OPTIONAL {
         ?adid schema:sameAs ?location .
         filter (strstarts(str(?adid),"http://kg.artsdata.ca/resource/K"))
     }
    OPTIONAL {
         ?location schema:sameAs ?adid .
         filter (strstarts(str(?adid),"http://kg.artsdata.ca/resource/K"))
     }
    OPTIONAL {
        values ?selectType {
           adr:ExhibitionEvent
        }
        ?event schema:additionalType ?additionalType .
        ?additionalType skos:broader*/skos:closeMatch* ?selectType
    }

}